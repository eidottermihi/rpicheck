buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "net.researchgate:gradle-release:2.6.0"
    }
}

apply plugin: "com.android.application"
apply plugin: "net.researchgate.release"
apply plugin: "com.github.hierynomus.license"

apply from: "$project.rootDir/gradle/license.gradle"
apply from: "gradle/release.gradle"

android {
    compileSdkVersion 28

    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "de.eidottermihi.raspicheck"
        minSdkVersion 21
        targetSdkVersion 28
        multiDexEnabled true
        versionName version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt"
        }

        debug {
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-project.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        warning "InvalidPackage", "MissingTranslation"
    }
}

dependencies {
    implementation project(":ssh")
    implementation "com.nononsenseapps:filepicker:3.1.0"
    implementation "io.freefair.android-colors:material:1.2.0"
    implementation "io.freefair.android-injection:injection:1.0.0-rc3"
    implementation "io.freefair.android-util:preference:1.1.1"
    implementation "com.android.support:design:28.0.0"
    implementation "org.ocpsoft.prettytime:prettytime:2.1.2.Final"
    implementation "org.slf4j:slf4j-api:$rootProject.ext.slf4jVersion"
    implementation("com.github.tony19:logback-android-classic:1.1.1-6") {
        // workaround issue #73
        exclude group: "com.google.android", module: "android"
    }
    implementation "com.google.code.findbugs:jsr305:2.0.2"

    androidTestImplementation "com.android.support:support-annotations:28.0.0"
    androidTestImplementation "com.android.support.test:runner:1.0.2"
    androidTestImplementation "com.android.support.test:rules:1.0.2"
    androidTestImplementation "org.hamcrest:hamcrest-library:1.3"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:3.0.1") {
        exclude group: "com.google.code.findbugs", module: "jsr305"
    }
    androidTestImplementation("com.android.support.test.espresso:espresso-intents:3.0.1") {
        exclude group: "com.google.code.findbugs", module: "jsr305"
    }


}

license {
    exclude "**/sheetrock/**"
}